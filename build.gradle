group 'com.cn.cly'
version '1.0-SNAPSHOT'

//当前根目录配置优先级高于子模块配置 如果此处配置与子模块配置有一致部分 则会覆盖子模块

buildscript { //构建脚本
    //定义版本号
    ext {
        springBootVersion = '2.7.1'
    }
    repositories {//jar包仓库
        maven { url 'https://maven.aliyun.com/repository/central' }
        maven { url "https://maven.aliyun.com/repository/gradle-plugin" }
        mavenLocal()
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
    }
}

subprojects {  //子项目通配
    apply plugin: 'java'
    apply plugin: 'idea'
    apply plugin: 'java-library'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'

    //编译使用UTF-8
    [compileJava, compileTestJava, javadoc]*.options*.encoding = 'UTF-8'

    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17

    configurations {
        compileOnly {
            extendsFrom annotationProcessor
        }
    }

    repositories {  //所有子项目jar包中央仓库
        maven { url 'https://maven.aliyun.com/repository/central' }
        maven { url "https://maven.aliyun.com/repository/gradle-plugin" }
        mavenLocal()
        mavenCentral()
    }

    //定义版本
    ext {
        ulisesVersion = '3.0.4'
        docVersion = '1.6.9'
        jsoupVersion = '1.14.3'
        nekohtmlVersion = '1.9.22'
        hutoolVersion = '5.8.3'
        guavaVersion = '31.1-jre'
        aliOssVersion = '3.15.0'
        druidVersion = '1.2.9'
        dialectVersion = '3.1.0'
        extrasVersion = '3.0.4.RELEASE'
        locatorVersion = '0.50'
        layuiVersion = '2.6.8'
        treegridVersion = '0.3.0'
        logstashVersion = '7.1.1'
    }

    dependencies {  //所有子项目通用依赖
        implementation 'org.springframework.boot:spring-boot-starter'
        implementation 'org.springframework.boot:spring-boot-starter-actuator'
        implementation 'org.springframework.boot:spring-boot-starter-web'
        implementation 'org.springframework.boot:spring-boot-starter-jersey'
        implementation 'org.springframework.boot:spring-boot-starter-security'
        implementation "com.github.ulisesbocchio:jasypt-spring-boot-starter:${ulisesVersion}"
        implementation "net.logstash.logback:logstash-logback-encoder:${logstashVersion}"

        developmentOnly 'org.springframework.boot:spring-boot-devtools'
        compileOnly 'org.projectlombok:lombok'
        annotationProcessor 'org.projectlombok:lombok'

        testImplementation 'org.springframework.boot:spring-boot-starter-test'
        testImplementation 'org.springframework.security:spring-security-test'
    }

    tasks.named('test') {
        useJUnitPlatform()
    }
}
